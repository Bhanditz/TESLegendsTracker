apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    packagingOptions.excludes = ['META-INF/services/javax.annotation.processing.Processor',
                                 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/README.txt',
                                 'LICENSE.txt', 'NOTICE.txt', 'README.txt', '.readme']
    defaultConfig {
        applicationId "com.ediposouza.teslesgendstracker"
        minSdkVersion prepareToRelease ? 16 : 21
        targetSdkVersion 25
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("boolean", "ENABLE_LOGS_IN_RELEASE", "false")
        buildConfigField("boolean", "PREPARE_TO_RELEASE", "$prepareToRelease")
        buildConfigField("String", "MIXPANEL_TOKEN", '"***REMOVED***"')
        buildConfigField("String", "GCM_SENDER", '"***REMOVED***"')
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
    }
    signingConfigs {
        release {
            try {
                def props = new Properties()
                props.load(file(propsFile).newInputStream())
                storeFile file(props.keystore_file)
                storePassword props.keystore_pass
                keyAlias "tes_legends_tracker"
                keyPassword props.tes_legends_tracker_pass
            } catch (e) {
                println(e.message)
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

ext {
    anko_version = "0.9"
    icePickVersion = "3.2.+"
    okIOVersion = "1.10.+"
    okHttpVersion = "3.4.+"
    playServicesVersion = prepareToRelease ? "10.0.1" : "9.8.0"
    supportLibraryVersion = "25.1.0"
    threetenabpVersion = "1.0.+"

    testMockitoVersion = "2.1.+"
    testRoboletricVersion = "3.1.+"
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
    resolutionStrategy.force "com.squareup.okio:okio:$okIOVersion"
    resolutionStrategy.force "com.squareup.okhttp3:okhttp:$okHttpVersion"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(name: 'clansFAB', ext: 'aar')
    compile(name: 'googleinapputil', ext: 'aar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.anko:anko-common:$anko_version"

    //Support
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:percent:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:multidex:1.0.1"

    //Google
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"
    compile "com.google.android.gms:play-services-gcm:$playServicesVersion"
    compile "com.google.firebase:firebase-auth:$playServicesVersion"
    compile "com.google.firebase:firebase-ads:$playServicesVersion"
    compile "com.google.firebase:firebase-core:$playServicesVersion"
    compile "com.google.firebase:firebase-config:$playServicesVersion"
    compile "com.google.firebase:firebase-database:$playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$playServicesVersion"
    releaseCompile "com.google.firebase:firebase-crash:$playServicesVersion"

    //Fabric
    compile("com.crashlytics.sdk.android:answers:1.3.9") {
        transitive = true
    }
    compile("com.crashlytics.sdk.android:crashlytics:2.6.2") {
        transitive = true
    }

    //Others
    compile "com.mixpanel.android:mixpanel-android:4.9.2"
    compile "com.jakewharton.threetenabp:threetenabp:$threetenabpVersion"
    compile "com.jakewharton.timber:timber:4.3.1"
    compile "org.greenrobot:eventbus:3.0.0"
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"
    compile "com.squareup.okio:okio:$okIOVersion"
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"

    compile "com.github.emanzanoaxa:RippleEffect:52ea2a0ab6"
    //    compile "com.github.traex.rippleeffect:library:1.3"
    compile "com.github.miguelbcr:TableFixHeaders-Wrapper:0.2.0"
    compile "com.timehop.stickyheadersrecyclerview:library:0.4.3"
    compile "jp.wasabeef:recyclerview-animators:2.2.4"

    testCompile "junit:junit:4.12"
    testCompile "com.jakewharton.threetenabp:threetenabp:$threetenabpVersion"
    testCompile "org.mockito:mockito-core:$testMockitoVersion"
    testCompile "org.robolectric:robolectric:$testRoboletricVersion"
    testCompile "org.robolectric:shadows-multidex:$testRoboletricVersion"

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'com.google.gms.google-services'
